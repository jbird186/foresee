#use "stdstr.4c"
#use "config.4c"

// strref -- hash
:hash {
    5381
    while over@c {
        dup 5 << + over@c +
        swap 1+ swap
    }
    nip
    if dup 0 < { -1* }
}

// slot_ptr -- bool
:pub slot_is_empty { @ not }
// slot_ptr -- bool
:pub slot_is_used { @ flag }
// idx map_ref slot_size -- slot_ptr
:pub map_idx { rot* + }

// key map_ref slot_size idx -- key map_ref slot_size idx slot_ptr bool
:_map_slot_skip {
    // -- key map_ref slot_size idx slot_ptr
    over over* 3 pick +
    if dup slot_is_empty {
        false
    } else {
        dup@ 5 pick strcmp flag
    }
}
// key map_ref slot_size -- slot_ptr
:pub map_get {
    2 pick hash MAP_SIZE %
    // -- key map_ref slot_size idx slot_ptr
    while _map_slot_skip {
        drop
        1+ MAP_SIZE %
    }
    nip nip nip nip
}
// key map_ref slot_size -- idx
:pub map_get_idx {
    2 pick hash MAP_SIZE %
    // -- key map_ref slot_size idx slot_ptr
    while _map_slot_skip {
        drop
        1+ MAP_SIZE %
    }
    drop nip nip nip
}
// key map_ref slot_size -- bool
:pub map_contains {
    map_get slot_is_used
}

// key scope map_ref slot_size idx -- key scope map_ref slot_size idx slot_ptr bool
:_scoped_map_slot_skip {
    // -- key scope map_ref slot_size idx slot_ptr
    over over* 3 pick +
    if dup slot_is_empty {
        false
    } else if dup@ 6 pick streq {
        dup->ScopedKey.scope@ 5 pick !=
    } else {
        true
    }
}
// key scope map_ref slot_size -- slot_ptr
:pub scoped_map_get {
    3 pick hash MAP_SIZE %
    // -- key scope map_ref slot_size idx slot_ptr
    while _scoped_map_slot_skip {
        drop
        1+ MAP_SIZE %
    }
    nip nip nip nip nip
}
// key scope map_ref slot_size -- idx
:pub scoped_map_get_idx {
    3 pick hash MAP_SIZE %
    // -- key scope map_ref slot_size idx slot_ptr
    while _scoped_map_slot_skip {
        drop
        1+ MAP_SIZE %
    }
    drop nip nip nip nip
}
// key scope map_ref slot_size -- bool
:pub scoped_map_contains {
    scoped_map_get slot_is_used
}
// name map_ref slot_size -- [idx, -1]
:pub scoped_map_find_idx {
    $int scope_idx: {&scope_stack stack_len}
    while &scope_idx@-- {
        #macro map_lookup {
            2 pick scope_idx SCOPE_STACK stack_get@ 3 pick 3 pick
        }
        if map_lookup scoped_map_contains {
            map_lookup scoped_map_get_idx
            nip nip nip
            return
        }
    }
    drop drop drop -1
}
// key map_ref slot_size -- slot_ptr
:pub scoped_map_find {
    // map_ref slot_size idx
    rot 2 pick 2 pick scoped_map_find_idx
    if dup is_success {
        rot rot map_idx
    } else {
        drop drop drop NULL
    }
}