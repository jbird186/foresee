#use "stdchar.4c"
#use "types.4c"
#use "stack.4c"

pub enum TKind {
    EOF
    INT
    CHAR
    STR
    IDENT
    WORD
    FN
    IF
    ELSE
    WHILE
    FOR
    CONTINUE
    BREAK
    COLON
    POUND
    COMMA
    STATIC
    VAR
    REF
    EQ
    ENUM
    STRUCT
    ARROW
    FIELD
    SIZE
    CALL
    PUB
    PAREN_START
    PAREN_END
    BRACE_START
    BRACE_END
    BRACKET_START
    BRACKET_END
    FILE_START
    FILE_END
}

// data kind file line ref --
pub :tok_push {
    // -- data kind file line ptr
    %Token stack_push
    swap over->Token.line!
    swap over->Token.file!
    swap over->Token.kind!c
    ->Token.data!
}

// src dest --
pub :tok_push_clone {
    // -- src ptr
    %Token stack_push
    over->Token.kind@c over->Token.kind!c
    over->Token.data@ over->Token.data!
    over->Token.file@ over->Token.file!
    swap->Token.line@ swap->Token.line!
}

pub :viewtk { dup->Token.kind@c }
pub :viewtd { dup->Token.data@ }
pub :viewtf { dup->Token.file@ }
pub :viewtl { dup->Token.line@ }

// tok_ptr -- tok_ptr
pub :show_tok {
    if viewtk TKind.EOF == { "EOF" puts }
    if viewtk TKind.INT == { viewtd put }
    if viewtk TKind.CHAR == {
        viewtd
        if dup isprint {
            "'" puts putc "'" puts
        } else {
            "char " puts put
        }
    }
    if viewtk TKind.STR == {
        "\"" puts
        var ptr c: {viewtd}
        while c@c {
            if c@c isprint { c@c putc }
            else if c@c '\0' == { "\\0" puts }     // null
            else if c@c '\t' == { "\\t" puts }     // tab
            else if c@c '\a' == { "\\a" puts }     // bell
            else if c@c '\b' == { "\\b" puts }     // backspace
            else if c@c '\n' == { "\\n" puts }     // newline
            else if c@c '\v' == { "\\v" puts }     // vertical tab
            else if c@c '\f' == { "\\f" puts }     // form feed
            else if c@c '\r' == { "\\r" puts }     // return
            else if c@c '\'' == { "\'" puts }      // single quote
            else if c@c '\"' == { "\\\"" puts }    // double quote
            else if c@c '\\' == { "\\\\" puts }    // backslash
            else { "\\?" puts }
            1 +=c
        }
        "\"" puts
    }
    if viewtk TKind.IDENT == { "identifier \""puts viewtd puts "\"" puts }
    if viewtk TKind.WORD == { "word \""puts viewtd puts "\"" puts }
    if viewtk TKind.FN == { ":fn" puts }
    if viewtk TKind.IF == { "if" puts }
    if viewtk TKind.ELSE == { "else" puts }
    if viewtk TKind.WHILE == { "while" puts }
    if viewtk TKind.FOR == { "for" puts }
    if viewtk TKind.CONTINUE == { "continue" puts }
    if viewtk TKind.BREAK == { "break" puts }
    if viewtk TKind.COLON == { ":" puts }
    if viewtk TKind.POUND == { "#" puts }
    if viewtk TKind.COMMA == { "," puts }
    if viewtk TKind.STATIC == { "static" puts }
    if viewtk TKind.VAR == { "$" puts }
    if viewtk TKind.REF == { "&" puts }
    if viewtk TKind.EQ == { viewtd puts }
    if viewtk TKind.ENUM == { "enum" puts }
    if viewtk TKind.STRUCT == { "struct" puts }
    if viewtk TKind.ARROW == { "->" puts }
    if viewtk TKind.FIELD == { "." puts }
    if viewtk TKind.SIZE == { "%" puts }
    if viewtk TKind.CALL == { "call" puts }
    if viewtk TKind.PUB == { "pub" puts }
    if viewtk TKind.PAREN_START == { "(" puts }
    if viewtk TKind.PAREN_END == { ")" puts }
    if viewtk TKind.BRACE_START == { "{" puts }
    if viewtk TKind.BRACE_END == { "}" puts }
    if viewtk TKind.BRACKET_START == { "[" puts }
    if viewtk TKind.BRACKET_END == { "]" puts }
    if viewtk TKind.FILE_START == { "FILE_START" puts }
    if viewtk TKind.FILE_END == { "FILE_END" puts }
}

// tok_ptr -- tok_ptr
pub :error {
    viewtf puts ":"puts viewtl put ": ERROR: "puts
}

#macro skip_delim(start, end) {(
    var ptr original_tok_ptr: {dup}
    // -- depth tok_ptr
    1 swap while %Token+ over {
        if viewtk not {
            original_tok_ptr error
            "Expected token of type "puts TKind.end put
            " after token of type "puts viewtk put
            "\n"puts
            1 exit
        }
        if viewtk TKind.start == {
            swap 1+ swap
        } else if viewtk TKind.end == {
            swap 1- swap
        }
    }
    // -- tok_ptr
    nip
)}

// tok_ptr -- new_tok_ptr
pub :skip_braces {
    skip_delim(BRACE_START, BRACE_END)
}

// tok_ptr -- new_tok_ptr
pub :skip_paren {
    skip_delim(PAREN_START, PAREN_END)
}

// tok_ptr -- new_tok_ptr
pub :skip_brackets {
    skip_delim(BRACKET_START, BRACKET_END)
}

// tok_ptr -- tok_ptr
#pub macro skip_until(until_tok_kind) {(
    dup
    while viewtk until_tok_kind != {
        if viewtk not {
            swap error
            "Expected token of type "puts until_tok_kind put
            " after token of type "puts viewtk put
            "\n"puts
            1 exit
        }
        else if viewtk TKind.PAREN_START == { skip_paren }
        else if viewtk TKind.BRACE_START == { skip_braces }
        else if viewtk TKind.BRACKET_START == { skip_brackets }
        else { %Token+ }
    }
    nip
    %Token+
)}