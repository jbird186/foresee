#include "std/stdstr.4c"
#include "src/config.4c"
#include "src/token.4c"

$ASM_LABEL_ID INT
$ASM_ROLL_ID INT
$ASM_FOPEN_ID INT

:parse_tok

// ops_dest tok_ptr --- ops_dest tok_ptr
#_parse_until(until_tok_kind) {
    while viewtk until_tok_kind != {
        over swap parse_tok
    }
    tok_next
}

// ops_dest tok_ptr --- new_tok_ptr
:parse_string {
    viewtd &inline_bufs push
    // tok_ptr
    &inline_bufs len 1- OP_PUSH_IBUF 3 roll toks_push
    tok_next
}
#define_intrinsic_opcode(data op_code alias) {
    if viewtd alias str_eq {
        // tok_ptr
        (data) op_code 3 roll toks_push
        tok_next
        return
    }
}
// ops_dest tok_ptr --- new_tok_ptr
:parse_ident {
    ////////// Intrinsics //////////
    // Misc / Special
    define_intrinsic_opcode(0 OP_NOOP "__OP_NOOP")
    define_intrinsic_opcode(0 OP_EXIT "__OP_EXIT")
    // Functions
    define_intrinsic_opcode(0 OP_RET "__OP_RET")
    // Stack Primitives
    define_intrinsic_opcode(0 OP_DROP "__OP_DROP")
    define_intrinsic_opcode(0 OP_PICK "__OP_PICK")
    define_intrinsic_opcode({&ASM_ROLL_ID@ dup 2+ &ASM_ROLL_ID!} OP_ROLL "__OP_ROLL")
    define_intrinsic_opcode(0 OP_DEPTH "__OP_DEPTH")
    // Reference Primitives
    define_intrinsic_opcode(0 OP_STORE "__OP_STORE")
    define_intrinsic_opcode(0 OP_FETCH "__OP_FETCH")
    // Binary Operations
    define_intrinsic_opcode(0 OP_ADD "__OP_ADD")
    define_intrinsic_opcode(0 OP_SUB "__OP_SUB")
    define_intrinsic_opcode(0 OP_MUL "__OP_MUL")
    define_intrinsic_opcode(0 OP_DIV "__OP_DIV")
    define_intrinsic_opcode(0 OP_AND "__OP_AND")
    define_intrinsic_opcode(0 OP_OR "__OP_OR")
    define_intrinsic_opcode(0 OP_XOR "__OP_XOR")
    define_intrinsic_opcode(0 OP_SHL "__OP_SHL")
    define_intrinsic_opcode(0 OP_SHR "__OP_SHR")
    define_intrinsic_opcode(0 OP_SAR "__OP_SAR")
    define_intrinsic_opcode(0 OP_NOT "__OP_NOT")
    define_intrinsic_opcode(0 OP_EQ "__OP_EQ")
    define_intrinsic_opcode(0 OP_GT "__OP_GT")
    define_intrinsic_opcode(0 OP_LT "__OP_LT")
    // I/O
    define_intrinsic_opcode({&ASM_FOPEN_ID@++} OP_FOPEN "__OP_FOPEN")
    define_intrinsic_opcode(0 OP_FREAD "__OP_FREAD")
    define_intrinsic_opcode(0 OP_FWRITE "__OP_FWRITE")
    define_intrinsic_opcode(0 OP_FCLOSE "__OP_FCLOSE")

    ////////// Functions //////////
    // ops_dest tok_ptr
    if viewtd &fns_map map_get not {
        "ERROR: Function '".s viewtd.s "' not defined\n".s
        1 exit
    }
    // tok_ptr
    viewtd OP_CALL 3 roll toks_push
    tok_next
}
// ops_dest tok_ptr if_id --- new_tok_ptr
:_parse_if_with_id
:_parse_if_with_id {
    // ops_dest tok_ptr if_id else_id
    &ASM_LABEL_ID@++
    // if_id else_id ops_dest tok_ptr
    3 roll 3 roll
    tok_next
    // Condition
    _parse_until(TOK_BRACE_START)
    // if_id else_id ops_dest tok_ptr
    2 pick OP_JZ 3 pick toks_push

    // Operations
    _parse_until(TOK_BRACE_END)

    // if_id else_id ops_dest tok_ptr
    3 pick OP_JMP 3 pick toks_push
    2 pick OP_LABEL 3 pick toks_push
    // if_id ops_dest tok_ptr
    rot drop

    // No else condition
    if viewtk TOK_ELSE != {
        swap drop
        // tok_ptr
        swap drop
        return
    }
    tok_next

    // Else condition
    // if_id ops_dest tok_ptr
    if viewtk TOK_BRACE_START == {
        tok_next
        _parse_until(TOK_BRACE_END)
        swap drop
        swap drop
    } else if viewtk TOK_IF == {
        // tok_ptr
        rot _parse_if_with_id
    } else {
        "ERROR: Invalid 'if' condition\n".s
        1 exit
    }
}
// ops_dest tok_ptr --- new_tok_ptr
:parse_if {
    // ops_dest if_id tok_ptr
    &ASM_LABEL_ID@++ swap
    // ops_dest if_id tok_ptr
    2 pick swap 2 pick _parse_if_with_id
    // tok_ptr
    swap OP_LABEL 3 roll toks_push
}
// ops_dest tok_ptr --- new_tok_ptr
:parse_while {
    tok_next

    // ops_dest tok_ptr start_id end_id
    &ASM_LABEL_ID@++ &ASM_LABEL_ID@++
    // start_id end_id ops_dest tok_ptr
    3 roll 3 roll
    // start_id end_id ops_dest tok_ptr
    3 pick OP_LABEL 3 pick toks_push

    // Condition
    _parse_until(TOK_BRACE_START)
    2 pick OP_JZ 3 pick toks_push

    // Operations
    _parse_until(TOK_BRACE_END)

    // end_id ops_dest tok_ptr
    3 roll OP_JMP 3 pick toks_push
    // tok_ptr
    rot OP_LABEL 3 roll toks_push

}
// ops_dest tok_ptr --- new_tok_ptr
:parse_colon {
    tok_next

    if viewtk TOK_IDENT != {
        "ERROR: Invalid function name\n".s
        1 exit
    }
    if viewtd &fns_map map_get dup (-1 !=) swap (0 !=) & {
        "ERROR: Function '".s viewtd.s "' already defined\n".s
        1 exit
    }

    // tok_ptr
    if swap {
        "ERROR: Function '".s viewtd.s "' must not define internal functions\n".s
        1 exit
    }

    // name tok_ptr
    viewtd swap tok_next
    // Function prototype
    if viewtk TOK_BRACE_START != {
        // tok_ptr
        swap -1 &fns_map map_insert
        return
    }

    // name tok_ptr fn_ptr
    &fn_ops_mem toks_head
    // tok_ptr
    rot swap &fns_map map_insert
    tok_next

    while viewtk TOK_BRACE_END != {
        &fn_ops_mem swap parse_tok
    }
    tok_next

    if &fn_ops_mem toks_head tok_prev tok_kind OP_RET != {
        0 OP_RET &fn_ops_mem toks_push
    }

    0 OP_EOF &fn_ops_mem toks_push
}
// ops_dest tok_ptr --- new_tok_ptr
:parse_dollar {
    // tok_ptr
    swap drop

    tok_next
    if viewtk TOK_IDENT != {
        "ERROR: Invalid buffer name\n".s
        1 exit
    }

    // Specify size
    // name tok_ptr
    viewtd swap tok_next
    if viewtk dup (TOK_INT ==) swap (TOK_BRACKET_START ==) | {
        if viewtk TOK_INT == {
            // name size tok_ptr
            viewtd swap tok_next
        } else {
            // name size tok_ptr
            1 swap while viewtk TOK_BRACKET_START == {
                tok_next
                // name size sum tok_ptr
                0 swap while viewtk TOK_INT == {
                    // name size sum tok_ptr
                    viewtd rot + swap
                    tok_next
                }
                if viewtk TOK_BRACKET_END != {
                    "ERROR: Invalid definition for buffer '".s 3 pick.s "'\n".s
                    1 exit
                }
                tok_next

                // name size tok_ptr sum
                swap
                if dup not {
                    "ERROR: Invalid definition for buffer '".s 3 pick.s "'\n".s
                    1 exit
                }
                // name size tok_ptr
                rot * swap
            }
        }
        // Initialize to String (optional)
        // name size tok_ptr
        if dup (viewtk TOK_COLON ==) swap (tok_next tok_kind TOK_STR ==) & {
            tok_next
            if viewtd str_len 2 pick > {
                "ERROR: Invalid size for buffer '".s 2 pick.s "'\n".s
                1 exit
            }
            // name size init tok_ptr
            viewtd swap
            tok_next
        } else {
            // name size init tok_ptr
            NULL swap
        }
    }
    // Initialize to String
    else if viewtk TOK_COLON == {
        tok_next
        if viewtk TOK_STR == {
            // name size tok_ptr
            viewtd str_len swap
            // name size init tok_ptr
            viewtd swap
            tok_next
        } else {
            "ERROR: Invalid initialization value for buffer '".s over.s "'\n".s
            1 exit
        }
    }
    else {
        "ERROR: Invalid definition for buffer '".s over.s "'\n".s
        1 exit
    }
    // name size tok_ptr
    3 pick rot &bufs_init_map map_insert
    // tok_ptr
    rot rot &bufs_size_map map_insert
}
// ops_dest tok_ptr --- new_tok_ptr
:parse_ref {
    tok_next
    if viewtk TOK_IDENT != {
        "ERROR: Invalid reference name\n".s
        1 exit
    }
    if viewtd &bufs_size_map map_get {
        viewtd OP_PUSH_BUF 3 roll toks_push
        tok_next
    } else {
        "ERROR: Buffer '".s viewtd.s "' not defined\n".s
        1 exit
    }
}

#parse_with(parse_kind parse_function) {
    else if viewtk parse_kind == {
        parse_function
    }
}

// ops_dest tok_ptr --- new_tok_ptr
:parse_tok {
    if viewtk TOK_POUND == {
        "ERROR: Unexpected preprocessing directive\n".s
        1 exit
    }
    else if viewtk TOK_WORD == {
        "ERROR: Macro '".s viewtd.s "' not defined\n".s
        1 exit
    }
    else if viewtk dup (TOK_INT ==) swap (TOK_CHAR ==) | {
        viewtd OP_PUSH_INT 3 roll toks_push
        tok_next
    }
    else if viewtk TOK_PAREN_START == {
        tok_next
        _parse_until(TOK_PAREN_END)
        swap drop
    }
    parse_with(TOK_STR parse_string)
    parse_with(TOK_IDENT parse_ident)
    parse_with(TOK_IF parse_if)
    parse_with(TOK_WHILE parse_while)
    parse_with(TOK_COLON parse_colon)
    parse_with(TOK_DOLLAR parse_dollar)
    parse_with(TOK_REF parse_ref)
    else {
        "ERROR: Unexpected token of type ".s viewtk. cr
        1 exit
    }
}

:parse_out_toks {
    // tok_ptr
    &out_toks_mem tok_first
    while viewtk TOK_EOF != {
        if viewtk dup (TOK_COLON !=) swap (TOK_DOLLAR !=) & {
            "ERROR: Unexpected token of type ".s viewtk. " outside of 'main'\n".s
            1 exit
        }
        0 swap parse_tok
    }
    drop

    if "main" &fns_map map_key_idx is_failure {
        "ERROR: Function 'main' not defined\n".s
        1 exit
    }

    "main" OP_CALL &start_ops_mem toks_push
    0 OP_PUSH_INT &start_ops_mem toks_push
    0 OP_EXIT &start_ops_mem toks_push
    0 OP_EOF &start_ops_mem toks_push
}