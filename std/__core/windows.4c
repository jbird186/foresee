#pub macro STDIN_FD {-10 win_GetStdHandle}
#pub macro STDOUT_FD {-11 win_GetStdHandle}
#pub macro STDERR_FD {-12 win_GetStdHandle}

// -- &argv argc
pub :__parse_args {
    #macro ptr_idx {%ptr* +}
    #macro wstrlen {NULL 0 win_WideCharToMultiByte}

    var int argc
    var ptr argv_wide: {win_GetCommandLineW &argc win_CommandLineToArgvW}
    if argv_wide is_failure { 1 exit }

    var int total_size: {argc %ptr*}
    for var int i: 0, i argc <, 1+=i {
        (argv_wide i ptr_idx@) wstrlen +=total_size
    }

    var ptr argv_char: {total_size win_LocalAlloc}
    if argv_char is_failure { 1 exit }

    var ptr str_ptr: {argv_char (argc %ptr*)+}
    for var int i: 0, i argc <, 1+=i {
        var int len: {(argv_wide i ptr_idx@) wstrlen}
        if (argv_wide i ptr_idx@) str_ptr len win_WideCharToMultiByte is_failure {
            1 exit
        }
        str_ptr (argv_char i ptr_idx)!
        len +=str_ptr
    }

    if argv_wide win_LocalFree is_failure { 1 exit }
    argv_char argc
}