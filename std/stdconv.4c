pub static char[24] __std_buf
static int_min_buf: "-9223372036854775808"

// int -- &buf len
pub :__write_int_to_temp {
    if dup INT_MIN == {
        drop &int_min_buf 20 return
    }

    0 &__std_buf 16+ !

    // -- is_neg int
    dup 0 < swap
    if over { -1* }

    // -- is_neg int len
    0 true while {
        &__std_buf 22+ over -
        rot dup 10 % '0' +
        rot !c
        10 /
        swap 1 +
        over
    }

    // -- int len
    if rot {
        '-' &__std_buf 22+ 2 pick - !c
        1+
    }

    // -- int &buf len
    &__std_buf 23+ over - swap
    rot drop
}

// int &buf --
pub :itoa {
    // -- buf &buf len
    swap __write_int_to_temp
    // -- len &buf buf
    swap rot
    while 2 pick {
        over@c over!
        1+ rot 1- rot 1+ rot
    }
    0 swap !c
    drop drop
}

// &buf -- int
pub :atoi {
    var ptr buf: {}
    var bool is_neg: {buf@c '-' ==}
    if is_neg { 1 +=buf }

    var int total: {0}
    while buf@c {
        (total 10*) (buf@c '0' -)+ =total
        1 +=buf
    }

    total if is_neg { -1* }
}